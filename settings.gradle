pluginManagement {
    repositories {
        mavenCentral()

        maven {
            url = "https://repo.screamingsandals.org/public/"
        }

        gradlePluginPortal()
    }
}

// The result will be for Java 11 but some dependencies requires Java 16 or Java 17
float javaVersion = Float.parseFloat(System.getProperty("java.class.version"))
if (javaVersion < 61.0) {
    throw new GradleException("ScreamingLib requires JDK 17 to compile")
}

rootProject.name = "ScreamingLib"

def registerModule(String name, String... platforms) {
    include(':' + name + '-common')
    project(':' + name + '-common').projectDir = file(name + '/common')
    platforms.each {
        include(':' + name + '-' + it)
        project(':' + name + '-' + it).projectDir = file(name + '/' + it)
    }
}

// Shared content
include 'api-utils'
registerModule('utils', 'adventure')
include 'nms'
include 'shared-core'
include 'lang'
include 'annotation'

// Main modules
registerModule('core', 'bukkit') //TODO: add others (minestom, sponge)
registerModule('proxy', 'bungee', 'velocity')

// Optional modules
registerModule('sidebar', 'bukkit') //TODO: add others
registerModule('healthindicator', 'bukkit') //TODO: add others
registerModule('npc', 'bukkit') //TODO: add others
registerModule('hologram', 'bukkit') //TODO: add others
registerModule('visuals-api', 'bukkit') //TODO: add others
registerModule('signs', 'bukkit') //TODO: add others
registerModule('placeholders', 'bukkit') //TODO: add others
registerModule('packets', 'vanilla', 'bukkit') //TODO: add others
registerModule('command', 'bukkit', /*'minestom' , 'sponge',*/ 'bungee', 'velocity')

