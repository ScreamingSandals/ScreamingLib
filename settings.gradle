pluginManagement {
    repositories {
        mavenCentral()

        maven {
            url = "https://repo.screamingsandals.org/public/"
        }

        gradlePluginPortal()
    }
}

// The result will be for Java 11 but some dependencies requires Java 16 or Java 17
float javaVersion = Float.parseFloat(System.getProperty("java.class.version"))
if (javaVersion < 61.0) {
    throw new GradleException("ScreamingLib requires JDK 17 to compile")
}

rootProject.name = "ScreamingLib"

def registerModule(String name, String... platforms) {
    include(':' + name + '-common')
    project(':' + name + '-common').projectDir = file(name + '/common')
    platforms.each {
        include(':' + name + '-' + it)
        project(':' + name + '-' + it).projectDir = file(name + '/' + it)
    }
}

def registerExtension(String name, String... platforms) {
    include(':' + name + '-common')
    project(':' + name + '-common').projectDir = file('extensions/' + name + '/common')
    platforms.each {
        include(':' + name + '-' + it)
        project(':' + name + '-' + it).projectDir = file('extensions/' + name + '/' + it)
    }
}

// Shared content
include 'paperlib' // relocation
include 'api-utils'
registerModule('utils')
include 'nms'
include 'minitag'
include 'nbt'
include 'shared-core'
include 'annotation'
registerModule('spectator', 'adventure', 'bungee')

// Main modules
registerModule('core', 'bukkit'/*, 'minestom'*/) //TODO: add others (minestom, sponge)
registerModule('proxy', 'bungee', 'velocity')

// Extensions (Optional modules)
include ':lang'
project(':lang').projectDir = file('extensions/lang')
include ':kotlin-extra'
project(':kotlin-extra').projectDir = file('extensions/kotlin-extra')
registerExtension('sidebar', 'bukkit') //TODO: add others
registerExtension('healthindicator', 'bukkit') //TODO: add others
registerExtension('npc', 'bukkit') //TODO: add others
registerExtension('hologram', 'bukkit') //TODO: add others
registerExtension('visuals-api', 'bukkit') //TODO: add others
registerExtension('signs', 'bukkit') //TODO: add others
registerExtension('placeholders', 'bukkit') //TODO: add others
registerExtension('economy', 'bukkit')
registerExtension('packets', 'vanilla', 'bukkit') //TODO: add others
registerExtension('ai', 'bukkit')
registerExtension('cloud', 'bukkit', /*'minestom' , 'sponge',*/ 'bungee', 'velocity', 'extras')

