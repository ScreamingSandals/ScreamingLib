rootProject.name = "ScreamingLib"

include 'api-utils'
include 'sender'
include 'screaming-annotation'
include 'signs'
include 'lang'
include 'visuals-api'
include 'results'

def registerModule(String name, String... platforms) {
    include(':' + name + '-common')
    project(':' + name + '-common').projectDir = file(name + '/common')
    platforms.each {
        include(':' + name + '-' + it)
        project(':' + name + '-' + it).projectDir = file(name + '/' + it)
    }
}

registerModule('utils', 'bukkit', 'minestom', 'sponge', 'adventure')
registerModule('material', 'bukkit', 'minestom', 'sponge')
registerModule('world', 'bukkit', 'minestom', 'sponge')
registerModule('player', 'bukkit', 'minestom', 'sponge')
registerModule('entity', 'bukkit', 'minestom', 'sponge')
registerModule('hologram', 'bukkit') //TODO: add others
registerModule('sidebar', 'bukkit') //TODO: add others
registerModule('healthindicator', 'bukkit') //TODO: add others
registerModule('placeholders', 'bukkit') //TODO: add others
registerModule('packets', 'bukkit') //TODO: add others
registerModule('gamecore', 'bukkit') //TODO: add others
registerModule('proxiedplayer', 'bungee', 'velocity')
registerModule('plugin', 'bukkit', 'minestom', 'sponge', 'bungee', 'velocity')
registerModule('tasker', 'bukkit', 'minestom', 'sponge', 'bungee', 'velocity')
registerModule('command', 'bukkit', 'minestom', 'sponge', 'bungee', 'velocity')
registerModule('event', 'bukkit', 'sponge', 'bungee', 'velocity')

