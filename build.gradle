plugins {
    id 'org.screamingsandals.plugin-builder' version "${SCREAMING_PLUGIN_BUILDER_VERSION}" apply false
    id 'org.screamingsandals.nms-mapper' version "${NMS_MAPPER_VERSION}" apply false
}

defaultTasks 'clean', 'screamCompile'//, 'allowJavadocUpload'

allprojects {
    group = 'org.screamingsandals.lib'
    version = SCREAMING_LIB_VERSION
}

subprojects {
    // TODO: find a way how to fill this list from settings.gradle
    if (project.name in ['sidebar-bukkit', 'healthindicator-bukkit', 'npc-bukkit', 'hologram-bukkit', 'visuals-api-bukkit',  'signs-bukkit']) {
        project.ext['onlyPomArtifact'] = true

        apply plugin: 'org.screamingsandals.plugin-builder-lite'
    } else {
        apply plugin: 'java'
        apply plugin: 'java-library'
        apply plugin: 'org.screamingsandals.plugin-builder'
    }

    java {
        disableAutoTargetJvm()
    }

    repositories {
        minecraftLibraries()
        jitpack()
        sponge()
        velocity()
        placeholderApi()
    }

    sourceCompatibility = JavaVersion.VERSION_11
}
